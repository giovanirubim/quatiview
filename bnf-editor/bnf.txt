int ::= /\d+/
char ::= /'([^\s\\]|\x20|\\[^\s])'/
id ::= /[\d_a-z]\w*/i
string ::= /"([^"\n\\]|\\(.|\n))*"/
nil ::= 'NULL'
constant::= nil | int | char | string | sizeof
int_type ::= ['unsigned'] signed_type
signed_type ::= 'int' | 'char'
ord_cmp ::= '>=' | '<=' | '>' | '<'
sizeof ::= 'sizeof' '(' (raw_type ('*')* | expr) ')'
expr ::= expr_a ('||' expr_a)*
expr_a ::= expr_b ('&&' expr_b)*
expr_b ::= expr_c (('==' | '!=') expr_c)*
expr_c ::= expr_d (ord_cmp expr_d)*
expr_d ::= expr_e (('+' | '-') expr_e)*
expr_e ::= expr_f (('*' | '/' | '%') expr_f)*
expr_f ::= fn_call | constant | '(' expr ')' | ['&'] l_val
fn_call ::= id '(' [expr (',' expr)*] ')'
l_val ::= (id | ptr_access) (index_access | attr_access)*
ptr_access ::= '*' (ptr_access | id | '(' expr ')')
index_access ::= '[' expr ']'
attr_access ::= ('->' | '.') id
var_def ::= var_list ';'
var_list ::= raw_type var_item (',' var_item)*
struct_def ::= 'struct' id '{' var_def+ '}' ';'
fn_def ::= raw_type ('*')* id '(' arg_list ')' scope
arg_item ::= raw_type var_item
arg_list ::= [arg_item (',' arg_item)*]
assign ::= l_val '=' expr ';'
exec_line ::= var_def | expr ';' | while | if | assign
if ::= 'if' '(' expr ')' scope ['else' scope]
ord_cmp ::= '>=' | '<=' | '>' | '<'
scope ::= '{' exec_line* '}'
raw_type ::= int_type | id
var_item ::= '*'* id array_size*
array_size ::= ('[' [expr] ']')
while ::= 'while' '(' expr ')' scope
program ::= struct_def | var_def | fn_def
